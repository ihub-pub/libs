/*
 * Copyright (c) 2021 Henry 李恒 (henry.box@outlook.com).
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java-platform'
    id 'pub.ihub.plugin'
    id 'pub.ihub.plugin.ihub-git-hooks'
    id 'pub.ihub.plugin.ihub-java' apply false
    id 'pub.ihub.plugin.ihub-verification' apply false
    id 'pub.ihub.plugin.ihub-publish'
}

subprojects {
    if (project in [project(':ihub-bom')/*, project(':ihub-sso')*/]) {
        return
    }
    apply {
        plugin 'pub.ihub.plugin.ihub-java'
        plugin 'pub.ihub.plugin.ihub-test'
        plugin 'pub.ihub.plugin.ihub-verification'
        plugin 'pub.ihub.plugin.ihub-publish'
    }
    iHubBom.bomVersions.clear()
    dependencies {
        platform(rootProject).with {
            implementation it
            pmd it
            annotationProcessor it
            testAnnotationProcessor it
        }
        if (project != project(':ihub-core')) {
            api project(':ihub-core')
        }
        if (project.name.endsWith('-spring-boot-starter')) {
            implementation 'org.springframework.boot:spring-boot-autoconfigure'
            annotationProcessor project(':ihub-process:ihub-process-boot')
        }
        annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'
    }
}

iHubGitHooks {
    hooks = [
        'pre-commit': './gradlew build',
        'commit-msg': './gradlew commitCheck'
    ]
}

//<editor-fold desc="组件依赖版本管理">

javaPlatform {
    allowDependencies()
}

iHubBom.bomVersions.clear()

dependencies {
    // 导入bom配置（优先级由上至下）
    api platform(project(':ihub-bom')),
//        platform('pub.ihub.keel:ihub-bom:main-SNAPSHOT'), TODO
        platform('org.jmolecules:jmolecules-bom:2022.2.3'),
//        platform('org.springframework.experimental:spring-modulith-bom:0.1.0'),
        platform('cn.hutool:hutool-bom:5.8.12'),
        platform('org.mockito:mockito-bom:5.1.1'),
        platform('org.spockframework:spock-bom:2.3-groovy-4.0'),
        platform('org.springframework.statemachine:spring-statemachine-bom:3.2.0'),
        platform('org.springframework.cloud:spring-cloud-dependencies:2022.0.1'),
        platform('com.alibaba.cloud:spring-cloud-alibaba-dependencies:2021.1'),
        platform('de.codecentric:spring-boot-admin-dependencies:3.0.0'),
        platform('com.google.guava:guava-bom:31.1-jre'),
        platform('com.google.inject:guice-bom:5.1.0'),
        platform('org.axonframework:axon-bom:4.7.0'),
        platform('org.springframework.boot:spring-boot-dependencies:3.0.3'),
        platform('io.quarkus:quarkus-bom:2.16.3.Final'),
        platform('org.springdoc:springdoc-openapi:2.0.2'),
        platform('cn.dev33:sa-token-dependencies:1.34.0')
    // 配置组件版本
    constraints {
        api 'com.alibaba.fastjson2:fastjson2:2.0.24',
            'com.alibaba.p3c:p3c-pmd:2.1.1',
            'com.baomidou:mybatis-plus-annotation:3.5.3.1',
            'com.baomidou:mybatis-plus-core:3.5.3.1',
            'com.baomidou:mybatis-plus:3.5.3.1',
            'com.baomidou:mybatis-plus-extension:3.5.3.1',
            'com.baomidou:mybatis-plus-boot-starter:3.5.3.1',
            'com.baomidou:mybatis-plus-generator:3.5.3.1',
            'com.athaydes:spock-reports:2.4.0-groovy-4.0',
            'io.swagger.core.v3:swagger-annotations:2.2.8',
            'com.google.auto.service:auto-service:1.0.1',
            'com.google.testing.compile:compile-testing:0.21.0',
            'net.ltgt.gradle.incap:incap:1.0.0',
            'net.ltgt.gradle.incap:incap-processor:1.0.0',
            'com.squareup:javapoet:1.13.0',
            'org.mapstruct:mapstruct:1.5.3.Final',
            'org.mapstruct:mapstruct-processor:1.5.3.Final',
            'me.zhyd.oauth:JustAuth:1.16.5'
    }
}

//</editor-fold>
