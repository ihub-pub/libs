/**
 +---------+
 | Plugins |
 +---------+
 */
plugins {
    id 'com.github.ben-manes.versions'      version "$uptodate_version"
    id 'com.palantir.git-version'           version "$palantir_gitversion_version"
    id 'io.freefair.lombok'                 version "$freefair_lombok_version"
    id 'io.spring.dependency-management'    version "$spring_io_dep_version"
    id 'org.springframework.boot'           version "$spring_boot_starter_version"
}

ext.getSystemProperty = { String name ->
    System.getProperty(name) ?: System.getenv(name.replaceAll(/\.|-/, '_').toUpperCase())
}

/**
 +----------------+
 | Build Settings |
 +----------------+
 */
subprojects {
    apply {
        from rootProject.file('gradle/default_plugins.gradle')
        from rootProject.file('gradle/default_deps.gradle')
        from rootProject.file('gradle/java_settings.gradle')
        from rootProject.file('gradle/spring_project_settings.gradle')
        from rootProject.file('gradle/verification_settings.gradle')
        if (project.name.startsWith(project_name)) {
            from rootProject.file('gradle/publishing_settings.gradle')
        }
    }

    dependencies {
        implementation "org.slf4j:slf4j-api:$slf4j_version"
    }
}
