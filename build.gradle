/*
 * Copyright (c) 2021 Henry 李恒 (henry.box@outlook.com).
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'pub.ihub.plugin'
}

subprojects {
    if (project.name == 'ihub-bom') {
        return
    }
    apply {
        plugin 'pub.ihub.plugin.ihub-java'
        plugin 'pub.ihub.plugin.ihub-test'
        plugin 'pub.ihub.plugin.ihub-verification'
        plugin 'pub.ihub.plugin.ihub-publish'
    }
    iHubBom.bomVersions.clear()
    dependencies {
        platform(rootProject).with {
            api it
            pmd it
            annotationProcessor it
        }
        if (project.name != 'ihub-core') {
            api project(':ihub-core')
        }
        if (project.name.endsWith('-spring-boot-starter')) {
            implementation 'org.springframework.boot:spring-boot-autoconfigure'
        }
        annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'
    }
}

apply {
    plugin 'java-platform'
    plugin 'pub.ihub.plugin.ihub-publish'
    plugin 'pub.ihub.plugin.ihub-git-hooks'
}

iHubGitHooks {
    hooks = [
        'pre-commit': './gradlew build'
    ]
}

//<editor-fold desc="组件依赖版本管理">

javaPlatform {
    allowDependencies()
}

iHubBom.bomVersions.clear()

dependencies {
    // 导入bom配置（优先级由上至下）
    api platform(project(':ihub-bom')),
//        platform('pub.ihub.keel:ihub-bom:main-SNAPSHOT'), TODO
        platform('org.jmolecules:jmolecules-bom:2021.2.1'),
        platform('io.github.openfeign:feign-bom:11.8'),
        platform('io.sentry:sentry-bom:5.5.2'),
        platform('cn.hutool:hutool-bom:5.7.17'),
        platform('com.squareup.okhttp3:okhttp-bom:4.9.3'),
        platform('org.spockframework:spock-bom:2.0-groovy-3.0'),
        platform('org.springframework.statemachine:spring-statemachine-bom:3.0.1'),
        platform('org.springframework.cloud:spring-cloud-dependencies:2021.0.0'),
        platform('com.alibaba.cloud:spring-cloud-alibaba-dependencies:2021.1'),
        platform('de.codecentric:spring-boot-admin-dependencies:2.5.5'),
        platform('com.google.guava:guava-bom:31.0.1-jre'),
        platform('com.google.inject:guice-bom:5.0.1'),
        platform('com.github.xiaoymin:knife4j-dependencies:3.0.3'),
        platform('org.springframework.boot:spring-boot-dependencies:2.6.2'),
        platform('org.apache.kylin:kylin:4.0.0')
    // 配置组件版本
    constraints {
        api 'com.alibaba:fastjson:1.2.79',
            'com.alibaba:druid:1.2.8',
            'com.alibaba:druid-spring-boot-starter:1.2.8',
            'com.alibaba.p3c:p3c-pmd:2.1.1',
            'com.baomidou:mybatis-plus-annotation:3.5.0',
            'com.baomidou:mybatis-plus-core:3.5.0',
            'com.baomidou:mybatis-plus:3.5.0',
            'com.baomidou:mybatis-plus-extension:3.5.0',
            'com.baomidou:mybatis-plus-boot-starter:3.5.0',
            'com.baomidou:mybatis-plus-generator:3.5.1',
            'com.github.xiaoymin:knife4j-aggregation-spring-boot-starter:2.0.9',
            'org.codehaus.groovy:groovy-all:3.0.9',
            'com.athaydes:spock-reports:2.1.1-groovy-3.0',
            'cn.hutool:hutool-all:5.7.18',
            'org.mapstruct:mapstruct:1.4.2.Final',
            'org.mapstruct:mapstruct-processor:1.4.2.Final'
    }
}

//</editor-fold>
